#!/bin/csh

module purge
module load comp/intel-12.0.4
#module load comp/gcc-5.3
module load comp/gcc-8.2

set logfile=npb-run.log
touch $logfile
set tmpf=npb.tmp.$$

echo "Date: `date`" >> $logfile
echo "Host: `hostname`" >> $logfile
module list >>& $logfile
echo "" >> $logfile

set cnt=0
set cntf=0
set cntp=0
set cntw=0

setenv NPB_TIMER_FLAG 1

foreach cf (gcc itc_p pgi)

set bindir=bin_$cf
set outdir=outs_$cf
if ( ! -e $outdir ) mkdir -p $outdir

set ntot=4

foreach n1 (4 2 1)
set n2=`expr $ntot / $n1`
foreach c (A)
foreach ap (bt-mz lu-mz sp-mz)
   set pgm=${ap}.${c}.x
   set pgmx=$bindir/$pgm
   set case="run $cf/$pgm n1xn2=${n1}x$n2"
   @ cnt++
   if ( -e $pgmx ) then
      set outf=$outdir/${ap}.${c}.out.${n1}x$n2
      touch $outf
      env OMP_NUM_THREADS=$n1,$n2 $pgmx >&! $tmpf
      grep -i ' successful' $tmpf >& /dev/null
      if ( $status == 0 ) then
         grep -i warning $tmpf >& /dev/null
         if ( $status == 0 ) then
            echo ">*> $case - successful+warning" | tee -a $logfile
            @ cntw++
         else
            echo ">>> $case - successful" | tee -a $logfile
         endif
      else
         echo "*** $case - FAILED" | tee -a $logfile
         @ cntf++
      endif
      cat $tmpf >> $outf
      \rm $tmpf
   else
      echo "... $case - not present" | tee -a $logfile
      @ cntp++
   endif
end
end
end

end

echo "" >> $logfile
echo "Date: `date`" >> $logfile
echo "Total number of cases: $cnt" | tee -a $logfile
echo "Total number of warned cases: $cntw" | tee -a $logfile
echo "Total number of FAILED cases: $cntf" | tee -a $logfile
echo "Total number of not present cases: $cntp" | tee -a $logfile
echo "" >> $logfile
